;;
;; Configurations
;;

relation Step: config ~> config                 hint(show "E")
relation Step_pure: admininstr* ~> admininstr*  hint(show "E")
relation Step_read: config ~> admininstr*       hint(show "E")
relation Steps: config ~>* config               hint(show "E")

rule Step/pure:
  z; instr*  ~>  z; instr'*
  -- Step_pure: instr* ~> instr'*

rule Step/read:
  z; instr*  ~>  z; instr'*
  -- Step_read: z; instr* ~> instr'*

rule Steps/refl:
  z; admininstr* ~>* z; admininstr*

rule Steps/trans:
  z; admininstr*  ~>*  z''; admininstr''*
  -- Step: z; admininstr*  ~>  z'; admininstr'*
  -- Steps: z'; admininstr'  ~>*  z''; admininstr''*


;; Expressions

relation Eval_expr: state; expr ~>* state; val*  hint(show "E-expr")

rule Eval_expr:
  z; instr*  ~>*  z'; val*
  -- Steps: z; instr*  ~>*  z'; val*


;;
;; Instructions
;;

;; Polymorphic instructions

rule Step_pure/unreachable:
  UNREACHABLE  ~>  TRAP

rule Step_pure/nop:
  NOP  ~>  eps

rule Step_pure/drop:
  val DROP  ~>  eps


rule Step_pure/select-true:
  val_1 val_2 (CONST I32 c) (SELECT t*?)  ~>  val_1
  -- if c =/= 0

rule Step_pure/select-false:
  val_1 val_2 (CONST I32 c) (SELECT t*?)  ~>  val_2
  -- if c = 0


;; Block instructions

def $blocktype(state, blocktype) : functype  hint(show $blocktype_(%,%))
def $blocktype(z, _RESULT eps) = eps -> eps
def $blocktype(z, _RESULT t) = eps -> t
def $blocktype(z, _IDX x) = ft  -- Expand: $type(z, x) ~~ FUNC ft

rule Step_read/block:
  z; val^k (BLOCK bt instr*)  ~>  (LABEL_ n `{eps} val^k instr*)
  -- if $blocktype(z, bt) = t_1^k -> t_2^n

rule Step_read/loop:
  z; val^k (LOOP bt instr*)  ~>  (LABEL_ k `{LOOP bt instr*} val^k instr*)
  -- if $blocktype(z, bt) = t_1^k -> t_2^n

rule Step_pure/if-true:
  (CONST I32 c) (IF bt instr_1* ELSE instr_2*)  ~>  (BLOCK bt instr_1*)
  -- if c =/= 0

rule Step_pure/if-false:
  (CONST I32 c) (IF bt instr_1* ELSE instr_2*)  ~>  (BLOCK bt instr_2*)
  -- if c = 0


rule Step_pure/label-vals:
  (LABEL_ n `{instr*} val*)  ~>  val*


;; Branch instructions

;; TODO: may want a label context instead of bubbling up
rule Step_pure/br-zero:
  (LABEL_ n `{instr'*} val'* val^n (BR 0) instr*)  ~>  val^n instr'*

rule Step_pure/br-succ:
  (LABEL_ n `{instr'*} val* (BR $(l+1)) instr*)  ~>  val* (BR l)


rule Step_pure/br_if-true:
  (CONST I32 c) (BR_IF l)  ~>  (BR l)
  -- if c =/= 0

rule Step_pure/br_if-false:
  (CONST I32 c) (BR_IF l)  ~>  eps
  -- if c = 0


rule Step_pure/br_table-lt:
  (CONST I32 i) (BR_TABLE l* l')  ~>  (BR l*[i])
  -- if i < |l*|

rule Step_pure/br_table-ge:
  (CONST I32 i) (BR_TABLE l* l')  ~>  (BR l')
  -- if i >= |l*|


rule Step_pure/br_on_null-null:
  val (BR_ON_NULL l) ~> (BR l)
  -- if val = REF.NULL ht

rule Step_pure/br_on_null-addr:
  val (BR_ON_NULL l) ~> val
  -- otherwise


rule Step_pure/br_on_non_null-null:
  val (BR_ON_NON_NULL l) ~> eps
  -- if val = REF.NULL ht

rule Step_pure/br_on_non_null-addr:
  val (BR_ON_NON_NULL l) ~> val (BR l)
  -- otherwise


rule Step_read/br_on_cast-succeed:
  z; ref (BR_ON_CAST l rt_1 rt_2)  ~>  ref (BR l)
  -- Ref_ok: $store(z) |- ref : rt
  -- Reftype_sub: {} |- rt <: $inst_reftype($moduleinst(z), rt_2)

rule Step_read/br_on_cast-fail:
  z; ref (BR_ON_CAST l rt_1 rt_2)  ~>  ref
  -- otherwise


rule Step_read/br_on_cast_fail-succeed:
  z; ref (BR_ON_CAST_FAIL l rt_1 rt_2)  ~>  ref
  -- Ref_ok: $store(z) |- ref : rt
  -- Reftype_sub: {} |- rt <: $inst_reftype($moduleinst(z), rt_2)

rule Step_read/br_on_cast_fail-fail:
  z; ref (BR_ON_CAST_FAIL l rt_1 rt_2)  ~>  ref (BR l)
  -- otherwise


;; Function instructions

rule Step_read/call:
  z; (CALL x)  ~>  (REF.FUNC_ADDR $funcaddr(z)[x]) (CALL_REF)

rule Step_read/call_ref-null:
  z; (REF.NULL ht) (CALL_REF x?)  ~>  TRAP

rule Step_read/call_ref-func:
  z; val^n (REF.FUNC_ADDR a) (CALL_REF x?)  ~>  (FRAME_ m `{f} (LABEL_ m `{eps} instr*))
  -- if $funcinst(z)[a] = fi
  -- Expand: fi.TYPE ~~ FUNC (t_1^n -> t_2^m)
  -- if fi.CODE = FUNC y (LOCAL t)* (instr*)
  -- if f = {LOCAL val^n ($default(t))*, MODULE fi.MODULE}


rule Step_read/return_call:
  z; (RETURN_CALL x)  ~>  (REF.FUNC_ADDR $funcaddr(z)[x]) (RETURN_CALL_REF)


rule Step_read/return_call_ref-frame-null:
  z; (FRAME_ k `{f} val* (REF.NULL ht) (RETURN_CALL_REF x?) instr*)  ~>  TRAP

rule Step_read/return_call_ref-frame-addr:
  z; (FRAME_ k `{f} val'* val^n (REF.FUNC_ADDR a) (RETURN_CALL_REF x?) instr*)  ~>  val^n (REF.FUNC_ADDR a) (CALL_REF x?)
  -- Expand: $funcinst(z)[a].TYPE ~~ FUNC (t_1^n -> t_2^m)

rule Step_read/return_call_ref-label:
  z; (LABEL_ k `{instr'*} val* (RETURN_CALL_REF x?) instr*)  ~>  val* (RETURN_CALL_REF x?)


rule Step_pure/call_indirect-call:
  (CALL_INDIRECT x y)  ~>  (TABLE.GET x) (REF.CAST (REF NULL $idx(y))) (CALL_REF y)

rule Step_pure/return_call_indirect:
  (RETURN_CALL_INDIRECT x y)  ~>  (TABLE.GET x) (REF.CAST (REF NULL $idx(y))) (RETURN_CALL_REF y)


rule Step_pure/frame-vals:
  (FRAME_ n `{f} val^n)  ~>  val^n

rule Step_pure/return-frame:
  (FRAME_ n `{f} val'* val^n RETURN instr*)  ~>  val^n

rule Step_pure/return-label:
  (LABEL_ k `{instr'*} val* RETURN instr*)  ~>  val* RETURN


;; Numeric instructions

rule Step_pure/unop-val:
  (CONST nt c_1) (UNOP nt unop)  ~>  (CONST nt c)
  -- if $unop(unop, nt, c_1) = c  ;; TODO

rule Step_pure/unop-trap:
  (CONST nt c_1) (UNOP nt unop)  ~>  TRAP
  -- if $unop(unop, nt, c_1) = eps  ;; TODO


rule Step_pure/binop-val:
  (CONST nt c_1) (CONST nt c_2) (BINOP nt binop)  ~>  (CONST nt c)
  -- if $binop(binop, nt, c_1, c_2) = c  ;; TODO

rule Step_pure/binop-trap:
  (CONST nt c_1) (CONST nt c_2) (BINOP nt binop)  ~>  TRAP
  -- if $binop(binop, nt, c_1, c_2) = eps  ;; TODO


rule Step_pure/testop:
  (CONST nt c_1) (TESTOP nt testop)  ~>  (CONST I32 c)
  -- if c = $testop(testop, nt, c_1)

rule Step_pure/relop:
  (CONST nt c_1) (CONST nt c_2) (RELOP nt relop)  ~>  (CONST I32 c)
  -- if c = $relop(relop, nt, c_1, c_2)


rule Step_pure/extend:
  (CONST nt c) (EXTEND nt n)  ~>  (CONST nt $ext(n, $size(nt), S, c))


rule Step_pure/cvtop-val:
  (CONST nt_1 c_1) (CVTOP nt_2 cvtop nt_1 sx?)  ~>  (CONST nt_2 c)
  -- if $cvtop(cvtop, nt_1, nt_2, sx?, c_1) = c  ;; TODO

rule Step_pure/cvtop-trap:
  (CONST nt_1 c_1) (CVTOP nt_2 cvtop nt_1 sx?)  ~>  TRAP
  -- if $cvtop(cvtop, nt_1, nt_2, sx?, c_1) = eps  ;; TODO


;; Vector instructions

rule Step_pure/vvunop:
  (VVCONST V128 cv_1) (VVUNOP V128 vvunop)  ~>  (VVCONST V128 cv)
  -- if $vvunop(vvunop, V128, cv_1) = cv ;; TODO


rule Step_pure/vvbinop:
  (VVCONST V128 cv_1) (VVCONST V128 cv_2) (VVBINOP V128 vvbinop)  ~>  (VVCONST V128 cv)
  -- if $vvbinop(vvbinop, V128, cv_1, cv_2) = cv ;; TODO


rule Step_pure/vvternop:
  (VVCONST V128 cv_1) (VVCONST V128 cv_2) (VVCONST V128 cv_3) (VVTERNOP V128 vvternop)  ~>  (VVCONST V128 cv)
  -- if $vvternop(vvternop, V128, cv_1, cv_2, cv_3) = cv ;; TODO


def $vzero: c_vectype ;; HACK for algorithmic backend
rule Step_pure/vvtestop:
  (VVCONST V128 cv_1) (VVTESTOP V128 (_VV ANY_TRUE)) ~> (CONST I32 i)
  -- if i = $ine(128, cv_1, $vzero)


rule Step_pure/swizzle:
  (VVCONST V128 cv_1) (VVCONST V128 cv_2) (SWIZZLE sh) ~> (VVCONST V128 cv')
  -- if sh = lnt X lns
  -- if i* = $lanes(sh, cv_2)
  -- if c* = $lanes(sh, cv_1) 0^(256 - lns)
  -- if $lanes(sh, cv') = c*[$(i*[k])]^(k<lns)


rule Step_pure/shuffle:
  (VVCONST V128 cv_1) (VVCONST V128 cv_2) (SHUFFLE sh laneidx*) ~> (VVCONST V128 cv)
  -- if sh = lnt X lns
  -- if i* = $lanes(sh, cv_1) $lanes(sh, cv_2)
  -- if $lanes(sh, cv) = i*[$(laneidx*[k])]^(k<lns)


rule Step_pure/splat:
  (CONST nt c_1) (SPLAT sh) ~> (VVCONST V128 cv)
  -- if nt = $unpacked(sh)
  -- if $lanes(sh, cv) = c_1^$dim(sh)


rule Step_pure/extract_lane-num:
  (VVCONST V128 cv_1) (EXTRACT_LANE sh laneidx) ~> (CONST nt c_2)
  -- if nt = $unpacked(sh)
  -- if sh = lnt X lns
  -- if c_2 = $ext($storagesize(lnt), $storagesize(nt), U, $lanes(sh, cv_1)[laneidx])

rule Step_pure/extract_lane-pack:
  (VVCONST V128 cv_1) (EXTRACT_LANE sh sx laneidx) ~> (CONST nt c_2)
  -- if nt = $unpacked(sh)
  -- if sh = lnt X lns
  -- if c_2 = $ext($storagesize(lnt), $storagesize(nt), sx, $lanes(sh, cv_1)[laneidx])


rule Step_pure/replace_lane:
  (VVCONST V128 cv_1) (CONST nt c_2) (REPLACE_LANE sh laneidx) ~> (VVCONST V128 cv)
  -- if i* = $lanes(sh, cv_1)
  -- if $lanes(sh, cv) = (i*)[[laneidx] = c_2]


rule Step_pure/vunop:
  (VVCONST V128 cv_1) (VUNOP sh vunop)  ~>  (VVCONST V128 cv)
  -- if cv = $vunop(vunop, sh, cv_1)


rule Step_pure/vbinop-val:
  (VVCONST V128 cv_1) (VVCONST V128 cv_2) (VBINOP sh vbinop)  ~>  (VVCONST V128 cv)
  -- if $vbinop(vbinop, sh, cv_1, cv_2) = cv

rule Step_pure/vbinop-trap:
  (VVCONST V128 cv_1) (VVCONST V128 cv_2) (VBINOP sh vbinop)  ~>  TRAP
  -- if $vbinop(vbinop, sh, cv_1, cv_2) = eps


rule Step_pure/vrelop:
  (VVCONST V128 cv_1) (VVCONST V128 cv_2) (VRELOP sh vrelop)  ~>  (VVCONST V128 cv)
  -- if i* = $lanes(sh, cv_1)
  -- if j* = $lanes(sh, cv_2)
  -- if sh = lnt X lns
  -- if $lanes(sh, cv) = $ext(1, $storagesize(lnt), S, $vrelop(vrelop, sh, i, j))*


rule Step_pure/vishiftop:
  (VVCONST V128 cv_1) (CONST I32 n) (VISHIFTOP sh vishiftop) ~> (VVCONST V128 cv)
  -- if sh = lnt X lns
  -- if i* = $lanes(sh, cv_1)
  -- if $lanes(sh, cv) = $vishiftop(vishiftop, lnt, i, n)*


rule Step_pure/all_true-true:
  (VVCONST V128 cv) (ALL_TRUE sh) ~> (CONST I32 1)
  -- if i_1* = $lanes(sh, cv)
  -- (if $(i_1 =/= 0))*

rule Step_pure/all_true-false:
  (VVCONST V128 cv) (ALL_TRUE sh) ~> (CONST I32 0)
  -- otherwise


rule Step_pure/bitmask:
  (VVCONST V128 cv) (BITMASK sh) ~> (CONST I32 i)
  -- if sh = lnt X lns
  -- if i_1^lns = $lanes(sh, cv)
  -- if $ibits(32, i) = $ilt(S, $storagesize(lnt), i_1, 0)^lns


rule Step_pure/narrow:
  (VVCONST V128 cv_1) (VVCONST V128 cv_2) (NARROW sh_2 sh_1 sx) ~> (VVCONST V128 cv)
  -- if sh_1 = lnt_1 X lns_1
  -- if sh_2 = lnt_2 X lns_2
  -- if i_1^lns_1 = $lanes(sh_1, cv_1)
  -- if i_2^lns_1 = $lanes(sh_1, cv_2)
  -- if n_1^lns_1 = $narrow($storagesize(lnt_1), $storagesize(lnt_2), sx, i_1)^lns_1
  -- if n_2^lns_1 = $narrow($storagesize(lnt_1), $storagesize(lnt_2), sx, i_2)^lns_1
  -- if $lanes(sh_2, cv) = n_1^lns_1 n_2^lns_1


rule Step_pure/vcvtop-normal:
  (VVCONST V128 cv_1) (VCVTOP sh_2 vcvtop eps sh_1 sx eps) ~> (VVCONST V128 cv)
  -- if sh_1 = lnt_1 X lns_1
  -- if sh_2 = lnt_2 X lns_2
  -- if i* = $lanes(sh_1, cv_1)
  -- if $lanes(sh_2, cv) = $vcvtop(vcvtop, $storagesize(lnt_1), $storagesize(lnt_2), sx, i)*


rule Step_pure/vcvtop-half:
  (VVCONST V128 cv_1) (VCVTOP sh_2 vcvtop hf sh_1 sx? eps) ~> (VVCONST V128 cv)
  -- if sh_1 = lnt_1 X lns_1
  -- if sh_2 = lnt_2 X lns_2
  -- if i* = $lanes(sh_1, cv_1)[$halfop(hf, 0, lns_2) : lns_2]
  -- if $lanes(sh_2, cv) = $vcvtop(vcvtop, $storagesize(lnt_1), $storagesize(lnt_2), sx?, i)*


rule Step_pure/vcvtop-zero:
  (VVCONST V128 cv_1) (VCVTOP sh_2 vcvtop eps sh_1 sx? ZERO) ~> (VVCONST V128 cv)
  -- if sh_1 = lnt_1 X lns_1
  -- if sh_2 = lnt_2 X lns_2
  -- if i* = $lanes(sh_1, cv_1)
  -- if $lanes(sh_2, cv) = $vcvtop(vcvtop, $storagesize(lnt_1), $storagesize(lnt_2), sx?, i)* 0^lns_1


rule Step_pure/dot:
  (VVCONST V128 cv_1) (VVCONST V128 cv_2) (DOT sh_1 sh_2 S) ~> (VVCONST V128 cv)
  -- if sh_1 = lnt_1 X lns_1
  -- if sh_2 = lnt_2 X lns_2
  -- if i_1 = $storagesize(lnt_1)
  -- if i_2 = $storagesize(lnt_2)
  -- if k_1^k' = $lanes(sh_2, cv_1)
  -- if k_2^k' = $lanes(sh_2, cv_2)
  -- if $concat_bytes((j_1 j_2)*) = $imul(i_1, $ext(i_2, i_1, S, k_1), $ext(i_2, i_1, S, k_2))^k'
  -- if j'* = $iadd(i_1, j_1, j_2)*
  -- if $lanes(sh_1, cv) = j'*


rule Step_pure/extmul_half:
  (VVCONST V128 cv_1) (VVCONST V128 cv_2) (EXTMUL_HALF sh_2 hf sh_1 sx) ~> (VVCONST V128 cv)
  -- if sh_1 = lnt_1 X lns_1
  -- if sh_2 = lnt_2 X lns_2
  -- if i^k = $lanes(sh_1, cv_1)[$halfop(hf, 0, lns_2) : lns_2]
  -- if j^k = $lanes(sh_1, cv_2)[$halfop(hf, 0, lns_2) : lns_2]
  -- if $lanes(sh_2, cv) = $imul($storagesize(lnt_2), $ext($storagesize(lnt_1), $storagesize(lnt_2), sx, i), $ext($storagesize(lnt_1), $storagesize(lnt_2), sx, j))^k


rule Step_pure/extadd_pairwise:
  (VVCONST V128 cv_1) (EXTADD_PAIRWISE sh_2 sh_1 sx) ~> (VVCONST V128 cv)
  -- if sh_1 = lnt_1 X lns_1
  -- if sh_2 = lnt_2 X lns_2
  -- if i^k = $lanes(sh_1, cv_1)
  -- if $concat_bytes((i_1 i_2)*) = $ext($storagesize(lnt_1), $storagesize(lnt_2), sx, i)^k
  -- if j* = $iadd($storagesize(lnt_2), i_1, i_2)*
  -- if $lanes(sh_2, cv) = j*

;; Reference instructions

rule Step_read/ref.func:
  z; (REF.FUNC x)  ~>  (REF.FUNC_ADDR $funcaddr(z)[x])

rule Step_pure/ref.i31:
  (CONST I32 i) REF.I31  ~>  (REF.I31_NUM $wrap(32, 31, i))


rule Step_pure/ref.is_null-true:
  val REF.IS_NULL  ~>  (CONST I32 1)
  -- if val = (REF.NULL ht)

rule Step_pure/ref.is_null-false:
  val REF.IS_NULL  ~>  (CONST I32 0)
  -- otherwise


rule Step_pure/ref.as_non_null-null:
  ref REF.AS_NON_NULL  ~>  TRAP
  -- if ref = (REF.NULL ht)

rule Step_pure/ref.as_non_null-addr:
  ref REF.AS_NON_NULL  ~>  ref
  -- otherwise


rule Step_pure/ref.eq-null:
  ref_1 ref_2 REF.EQ  ~>  (CONST I32 1)
  -- if ref_1 = REF.NULL ht_1 /\ ref_2 = REF.NULL ht_2

rule Step_pure/ref.eq-true:
  ref_1 ref_2 REF.EQ  ~>  (CONST I32 1)
  -- otherwise
  -- if ref_1 = ref_2

rule Step_pure/ref.eq-false:
  ref_1 ref_2 REF.EQ  ~>  (CONST I32 0)
  -- otherwise


rule Step_read/ref.test-true:
  z; ref (REF.TEST rt)  ~>  (CONST I32 1)
  -- Ref_ok: $store(z) |- ref : rt'
  -- Reftype_sub: {} |- rt' <: $inst_reftype($moduleinst(z), rt)

rule Step_read/ref.test-false:
  z; ref (REF.TEST rt)  ~>  (CONST I32 0)
  -- otherwise


rule Step_read/ref.cast-succeed:
  z; ref (REF.CAST rt)  ~>  ref
  -- Ref_ok: $store(z) |- ref : rt'
  -- Reftype_sub: {} |- rt' <: $inst_reftype($moduleinst(z), rt)

rule Step_read/ref.cast-fail:
  z; ref (REF.CAST rt)  ~>  TRAP
  -- otherwise


;; Scalar reference instructions

rule Step_pure/i31.get-null:
  (REF.NULL ht) (I31.GET sx)  ~>  TRAP

rule Step_pure/i31.get-num:
  (REF.I31_NUM i) (I31.GET sx)  ~>  (CONST I32 $ext(31, 32, sx, i))


;; Structure instructions

rule Step/struct.new:
  z; val^n (STRUCT.NEW x)  ~>  $ext_structinst(z, si); (REF.STRUCT_ADDR $(|$structinst(z)|))
  -- Expand: $type(z, x) ~~ STRUCT (mut zt)^n
  -- if si = {TYPE $type(z, x), FIELD ($packval(zt, val))^n}

rule Step_read/struct.new_default:
  z; (STRUCT.NEW_DEFAULT x)  ~>  val* (STRUCT.NEW x)
  -- Expand: $type(z, x) ~~ STRUCT (mut zt)*
  -- if ($default($unpacktype(zt)) = val)*


rule Step_read/struct.get-null:
  z; (REF.NULL ht) (STRUCT.GET sx? x i)  ~>  TRAP

rule Step_read/struct.get-struct:
  z; (REF.STRUCT_ADDR a) (STRUCT.GET sx? x i)  ~>  $unpackval(zt*[i], sx?, si.FIELD[i])
  -- if $structinst(z)[a] = si
  -- Expand: si.TYPE ~~ STRUCT (mut zt)*


rule Step/struct.set-null:
  z; (REF.NULL ht) val (STRUCT.SET x i)  ~>  z; TRAP

rule Step/struct.set-struct:
  z; (REF.STRUCT_ADDR a) val (STRUCT.SET x i)  ~>  $with_struct(z, a, i, fv); eps
  -- Expand: $structinst(z)[a].TYPE ~~ STRUCT (mut zt)*
  -- if fv = $packval(zt*[i], val)


;; Array instructions

rule Step_read/array.new:
  z; val (CONST I32 n) (ARRAY.NEW x)  ~>  val^n (ARRAY.NEW_FIXED x n)

rule Step_read/array.new_default:
  z; (CONST I32 n) (ARRAY.NEW_DEFAULT x)  ~>  val^n (ARRAY.NEW_FIXED x n)
  -- Expand: $type(z, x) ~~ ARRAY (mut zt)
  -- if $default($unpacktype(zt)) = val

rule Step/array.new_fixed:
  z; val^n (ARRAY.NEW_FIXED x n)  ~>  $ext_arrayinst(z, ai); (REF.ARRAY_ADDR $(|$arrayinst(z)|))
  -- Expand: $type(z, x) ~~ ARRAY (mut zt)
  -- if ai = {TYPE $type(z, x), FIELD ($packval(zt, val))^n}


rule Step_read/array.new_elem-oob:
  z; (CONST I32 i) (CONST I32 n) (ARRAY.NEW_ELEM x y)  ~>  TRAP
  -- if $(i + n > |$elem(z, y).ELEM|)

rule Step_read/array.new_elem-alloc:
  z; (CONST I32 i) (CONST I32 n) (ARRAY.NEW_ELEM x y)  ~>  ref^n (ARRAY.NEW_FIXED x n)
  -- if ref^n = $elem(z, y).ELEM[i : n]


rule Step_read/array.new_data-oob:
  z; (CONST I32 i) (CONST I32 n) (ARRAY.NEW_DATA x y)  ~>  TRAP
  -- Expand: $type(z, x) ~~ ARRAY (mut zt)
  -- if $(i + n * $storagesize(zt)/8 > |$data(z, y).DATA|)

rule Step_read/array.new_data-alloc:
  z; (CONST I32 i) (CONST I32 n) (ARRAY.NEW_DATA x y)  ~>  (CONST nt c)^n (ARRAY.NEW_FIXED x n)
  -- Expand: $type(z, x) ~~ ARRAY (mut zt)
  -- if nt = $unpacknumtype(zt)
  -- if $concat_bytes($ztbytes(zt, c)^n) = $data(z, y).DATA[i : n * $storagesize(zt)/8]


rule Step_read/array.get-null:
  z; (REF.NULL ht) (CONST I32 i) (ARRAY.GET sx? x)  ~>  TRAP

rule Step_read/array.get-oob:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) (ARRAY.GET sx? x)  ~>  TRAP
  -- if i >= |$arrayinst(z)[a].FIELD|

rule Step_read/array.get-array:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) (ARRAY.GET sx? x)  ~>  $unpackval(zt, sx?, fv)
  -- if fv = $arrayinst(z)[a].FIELD[i]
  -- Expand: $arrayinst(z)[a].TYPE ~~ ARRAY (mut zt)


rule Step/array.set-null:
  z; (REF.NULL ht) (CONST I32 i) val (ARRAY.SET x)  ~>  z; TRAP

rule Step/array.set-oob:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) val (ARRAY.SET x)  ~>  z; TRAP
  -- if i >= |$arrayinst(z)[a].FIELD|

rule Step/array.set-array:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) val (ARRAY.SET x)  ~>  $with_array(z, a, i, fv); eps
  -- Expand: $arrayinst(z)[a].TYPE ~~ ARRAY (mut zt)
  -- if fv = $packval(zt, val)


rule Step_read/array.len-null:
  z; (REF.NULL ht) ARRAY.LEN  ~>  TRAP

rule Step_read/array.len-array:
  z; (REF.ARRAY_ADDR a) ARRAY.LEN  ~>  (CONST I32 n)
  -- if n = |$arrayinst(z)[a].FIELD|


rule Step_read/array.fill-null:
  z; (REF.NULL ht) (CONST I32 i) val (CONST I32 n) (ARRAY.FILL x)  ~>  TRAP

rule Step_read/array.fill-oob:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) val (CONST I32 n) (ARRAY.FILL x)  ~>  TRAP
  -- if $(i + n > |$arrayinst(z)[a].FIELD|)

rule Step_read/array.fill-zero:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) val (CONST I32 n) (ARRAY.FILL x)  ~>  eps
  -- otherwise
  -- if n = 0

rule Step_read/array.fill-succ:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) val (CONST I32 n) (ARRAY.FILL x)  ~>
    (REF.ARRAY_ADDR a) (CONST I32 i) val (ARRAY.SET x)
    (REF.ARRAY_ADDR a) (CONST I32 $(i + 1)) val (CONST I32 $(n-1)) (ARRAY.FILL x)
  -- otherwise

rule Step_read/array.copy-null1:
  z; (REF.NULL ht_1) (CONST I32 i_1) ref (CONST I32 i_2) (CONST I32 n) (ARRAY.COPY x_1 x_2)  ~>  TRAP

rule Step_read/array.copy-null2:
  z; ref (CONST I32 i_1) (REF.NULL ht_2) (CONST I32 i_2) (CONST I32 n) (ARRAY.COPY x_1 x_2)  ~>  TRAP

rule Step_read/array.copy-oob1:
  z; (REF.ARRAY_ADDR a_1) (CONST I32 i_1) (REF.ARRAY_ADDR a_2) (CONST I32 i_2) (CONST I32 n) (ARRAY.COPY x_1 x_2)  ~>  TRAP
  -- if $(i_1 + n > |$arrayinst(z)[a_1].FIELD|)

rule Step_read/array.copy-oob2:
  z; (REF.ARRAY_ADDR a_1) (CONST I32 i_1) (REF.ARRAY_ADDR a_2) (CONST I32 i_2) (CONST I32 n) (ARRAY.COPY x_1 x_2)  ~>  TRAP
  -- if $(i_2 + n > |$arrayinst(z)[a_2].FIELD|)

rule Step_read/array.copy-zero:
  z; (REF.ARRAY_ADDR a_1) (CONST I32 i_1) (REF.ARRAY_ADDR a_2) (CONST I32 i_2) (CONST I32 n) (ARRAY.COPY x_1 x_2)  ~>  eps
  -- otherwise
  -- if n = 0

rule Step_read/array.copy-le:
  z; (REF.ARRAY_ADDR a_1) (CONST I32 i_1) (REF.ARRAY_ADDR a_2) (CONST I32 i_2) (CONST I32 n) (ARRAY.COPY x_1 x_2)  ~>
    (REF.ARRAY_ADDR a_1) (CONST I32 i_1)
    (REF.ARRAY_ADDR a_2) (CONST I32 i_2)
    (ARRAY.GET sx? x_2) (ARRAY.SET x_1)
    (REF.ARRAY_ADDR a_1) (CONST I32 $(i_1 + 1)) (REF.ARRAY_ADDR a_2) (CONST I32 $(i_2 + 1)) (CONST I32 $(n-1)) (ARRAY.COPY x_1 x_2)
  -- otherwise
  -- Expand: $type(z, x_2) ~~ ARRAY (mut zt_2)
  -- if sx? = $sxfield(zt_2)
  -- if i_1 <= i_2

rule Step_read/array.copy-gt:
  z; (REF.ARRAY_ADDR a_1) (CONST I32 i_1) (REF.ARRAY_ADDR a_2) (CONST I32 i_2) (CONST I32 n) (ARRAY.COPY x_1 x_2)  ~>
    (REF.ARRAY_ADDR a_1) (CONST I32 $(i_1 + n - 1))
    (REF.ARRAY_ADDR a_2) (CONST I32 $(i_2 + n - 1))
    (ARRAY.GET sx? x_2) (ARRAY.SET x_1)
    (REF.ARRAY_ADDR a_1) (CONST I32 i_1) (REF.ARRAY_ADDR a_2) (CONST I32 i_2) (CONST I32 $(n-1)) (ARRAY.COPY x_1 x_2)
  -- otherwise
  -- Expand: $type(z, x_2) ~~ ARRAY (mut zt_2)
  -- if sx? = $sxfield(zt_2)


rule Step_read/array.init_elem-null:
  z; (REF.NULL ht) (CONST I32 i) (CONST I32 j) (CONST I32 n) (ARRAY.INIT_ELEM x y)  ~>  TRAP

rule Step_read/array.init_elem-oob1:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) (CONST I32 j) (CONST I32 n) (ARRAY.INIT_ELEM x y)  ~>  TRAP
  -- if $(i + n > |$arrayinst(z)[a].FIELD|)

rule Step_read/array.init_elem-oob2:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) (CONST I32 j) (CONST I32 n) (ARRAY.INIT_ELEM x y)  ~>  TRAP
  -- if $(j + n > |$elem(z, y).ELEM|)

rule Step_read/array.init_elem-zero:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) (CONST I32 j) (CONST I32 n) (ARRAY.INIT_ELEM x y)  ~>  eps
  -- otherwise
  -- if n = 0

rule Step_read/array.init_elem-succ:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) (CONST I32 j) (CONST I32 n) (ARRAY.INIT_ELEM x y)  ~>
    (REF.ARRAY_ADDR a) (CONST I32 i) ref (ARRAY.SET x)
    (REF.ARRAY_ADDR a) (CONST I32 $(i + 1)) (CONST I32 $(j + 1)) (CONST I32 $(n-1)) (ARRAY.INIT_ELEM x y)
  -- otherwise
  -- if ref = $elem(z, y).ELEM[j]


rule Step_read/array.init_data-null:
  z; (REF.NULL ht) (CONST I32 i) (CONST I32 j) (CONST I32 n) (ARRAY.INIT_DATA x y)  ~>  TRAP

rule Step_read/array.init_data-oob1:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) (CONST I32 j) (CONST I32 n) (ARRAY.INIT_DATA x y)  ~>  TRAP
  -- if $(i + n > |$arrayinst(z)[a].FIELD|)

rule Step_read/array.init_data-oob2:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) (CONST I32 j) (CONST I32 n) (ARRAY.INIT_DATA x y)  ~>  TRAP
  -- Expand: $type(z, x) ~~ ARRAY (mut zt)
  -- if $(j + n * $storagesize(zt)/8 > |$data(z, y).DATA|)

rule Step_read/array.init_data-zero:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) (CONST I32 j) (CONST I32 n) (ARRAY.INIT_DATA x y)  ~>  eps
  -- otherwise
  -- if n = 0

rule Step_read/array.init_data-succ:
  z; (REF.ARRAY_ADDR a) (CONST I32 i) (CONST I32 j) (CONST I32 n) (ARRAY.INIT_DATA x y)  ~>
    (REF.ARRAY_ADDR a) (CONST I32 i) (CONST nt c) (ARRAY.SET x)
    (REF.ARRAY_ADDR a) (CONST I32 $(i + 1)) (CONST I32 $(j + $storagesize(zt)/8)) (CONST I32 $(n-1)) (ARRAY.INIT_DATA x y)
  -- otherwise
  -- Expand: $type(z, x) ~~ ARRAY (mut zt)
  -- if nt = $unpacknumtype(zt)
  -- if $ztbytes(zt, c) = $data(z, y).DATA[j : $storagesize(zt)/8]


;; External reference instructions

rule Step_pure/extern.convert_any-null:
  (REF.NULL ht) EXTERN.CONVERT_ANY  ~>  (REF.NULL EXTERN)

rule Step_pure/extern.convert_any-addr:
  addrref EXTERN.CONVERT_ANY  ~>  (REF.EXTERN addrref)


rule Step_pure/any.convert_extern-null:
  (REF.NULL ht) ANY.CONVERT_EXTERN  ~>  (REF.NULL ANY)

rule Step_pure/any.convert_extern-addr:
  (REF.EXTERN addrref) ANY.CONVERT_EXTERN  ~>  addrref


;; Local instructions

rule Step_read/local.get:
  z; (LOCAL.GET x)  ~>  val
  -- if $local(z, x) = val

rule Step/local.set:
  z; val (LOCAL.SET x)  ~>  $with_local(z, x, val); eps

rule Step_pure/local.tee:
  val (LOCAL.TEE x)  ~>  val val (LOCAL.SET x)


;; Global instructions

rule Step_read/global.get:
  z; (GLOBAL.GET x)  ~>  $global(z, x).VALUE

rule Step/global.set:
  z; val (GLOBAL.SET x)  ~>  $with_global(z, x, val); eps


;; Table instructions

rule Step_read/table.get-oob:
  z; (CONST I32 i) (TABLE.GET x)  ~>  TRAP
  -- if i >= |$table(z, x).ELEM|

rule Step_read/table.get-val:
  z; (CONST I32 i) (TABLE.GET x)  ~>  $table(z,x).ELEM[i]
  -- if i < |$table(z, x).ELEM|

rule Step/table.set-oob:
  z; (CONST I32 i) ref (TABLE.SET x)  ~>  z; TRAP
  -- if i >= |$table(z, x).ELEM|

rule Step/table.set-val:
  z; (CONST I32 i) ref (TABLE.SET x)  ~>  $with_table(z, x, i, ref); eps
  -- if i < |$table(z, x).ELEM|


rule Step_read/table.size:
  z; (TABLE.SIZE x)  ~>  (CONST I32 n)
  -- if |$table(z, x).ELEM| = n  ;; TODO: inline this


rule Step/table.grow-succeed:
  z; ref (CONST I32 n) (TABLE.GROW x)  ~>  $with_tableinst(z, x, ti); (CONST I32 $(|$table(z, x).ELEM|))
  -- if ti = $growtable($table(z, x), n, ref)

rule Step/table.grow-fail:
  z; ref (CONST I32 n) (TABLE.GROW x)  ~>  z; (CONST I32 $invsigned(32, $(-1)))


rule Step_read/table.fill-oob:
  z; (CONST I32 i) val (CONST I32 n) (TABLE.FILL x)  ~>  TRAP
  -- if $(i + n) > |$table(z, x).ELEM|

rule Step_read/table.fill-zero:
  z; (CONST I32 i) val (CONST I32 n) (TABLE.FILL x)  ~>  eps
  -- otherwise
  -- if n = 0

rule Step_read/table.fill-succ:
  z; (CONST I32 i) val (CONST I32 n) (TABLE.FILL x)  ~>
    (CONST I32 i) val (TABLE.SET x)
    (CONST I32 $(i+1)) val (CONST I32 $(n-1)) (TABLE.FILL x)
  -- otherwise


rule Step_read/table.copy-oob:
  z; (CONST I32 j) (CONST I32 i) (CONST I32 n) (TABLE.COPY x y)  ~>  TRAP
  -- if $(i + n) > |$table(z, y).ELEM| \/ $(j + n) > |$table(z, x).ELEM|

rule Step_read/table.copy-zero:
  z; (CONST I32 j) (CONST I32 i) (CONST I32 n) (TABLE.COPY x y)  ~>  eps
  -- otherwise
  -- if n = 0

rule Step_read/table.copy-le:
  z; (CONST I32 j) (CONST I32 i) (CONST I32 n) (TABLE.COPY x y)  ~>
    (CONST I32 j) (CONST I32 i) (TABLE.GET y) (TABLE.SET x)
    (CONST I32 $(j+1)) (CONST I32 $(i+1)) (CONST I32 $(n-1)) (TABLE.COPY x y)
  -- otherwise
  -- if j <= i

rule Step_read/table.copy-gt:
  z; (CONST I32 j) (CONST I32 i) (CONST I32 n) (TABLE.COPY x y)  ~>
    (CONST I32 $(j+n-1)) (CONST I32 $(i+n-1)) (TABLE.GET y) (TABLE.SET x)
    (CONST I32 j) (CONST I32 i) (CONST I32 $(n-1)) (TABLE.COPY x y)
  -- otherwise


rule Step_read/table.init-oob:
  z; (CONST I32 j) (CONST I32 i) (CONST I32 n) (TABLE.INIT x y)  ~>  TRAP
  -- if $(i + n) > |$elem(z, y).ELEM| \/ $(j + n) > |$table(z, x).ELEM|

rule Step_read/table.init-zero:
  z; (CONST I32 j) (CONST I32 i) (CONST I32 n) (TABLE.INIT x y)  ~>  eps
  -- otherwise
  -- if n = 0

rule Step_read/table.init-succ:
  z; (CONST I32 j) (CONST I32 i) (CONST I32 n) (TABLE.INIT x y)  ~>
    (CONST I32 j) $elem(z,y).ELEM[i] (TABLE.SET x)
    (CONST I32 $(j+1)) (CONST I32 $(i+1)) (CONST I32 $(n-1)) (TABLE.INIT x y)
  -- otherwise


rule Step/elem.drop:
  z; (ELEM.DROP x)  ~>  $with_elem(z, x, eps); eps


;; Memory instructions

rule Step_read/load-num-oob:
  z; (CONST I32 i) (LOAD nt x marg)  ~>  TRAP
  -- if $(i + marg.OFFSET + $size(nt)/8 > |$mem(z, x).DATA|)

rule Step_read/load-num-val:
  z; (CONST I32 i) (LOAD nt x marg)  ~>  (CONST nt c)
  -- if $ntbytes(nt, c) = $mem(z, x).DATA[i + marg.OFFSET : $size(nt)/8]

rule Step_read/load-pack-oob:
  z; (CONST I32 i) (LOAD nt (n _ sx) x marg)  ~>  TRAP
  -- if $(i + marg.OFFSET + n/8 > |$mem(z, x).DATA|)

rule Step_read/load-pack-val:
  z; (CONST I32 i) (LOAD nt (n _ sx) x marg)  ~>  (CONST nt $ext(n, $size(nt), sx, c))
  -- if $ibytes(n, c) = $mem(z, x).DATA[i + marg.OFFSET : n/8]

rule Step_read/vload-oob:
  z; (CONST I32 i) (VLOAD (LOAD marg) x)  ~>  TRAP
  -- if $(i + marg.OFFSET + $size(V128)/8 > |$mem(z, x).DATA|)

rule Step_read/vload-val:
  z; (CONST I32 i) (VLOAD (LOAD marg) x)  ~>  (VVCONST V128 cv)
  -- if $vtbytes(V128, cv) = $mem(z, x).DATA[i + marg.OFFSET : $size(V128)/8]

rule Step_read/vload-shape-oob:
  z; (CONST I32 i) (VLOAD (SHAPE (PACKSHAPE psl psr) sx marg) x)  ~>  TRAP
  -- if $(i + marg.OFFSET + psl * psr/8) > |$mem(z, x).DATA|

rule Step_read/vload-shape-val:
  z; (CONST I32 i) (VLOAD (SHAPE (PACKSHAPE psl psr) sx marg) x)  ~>  (VVCONST V128 cv)
  -- (if $ibytes(psl, m) = $mem(z, x).DATA[i + marg.OFFSET + k * psl/8 : psl/8])^(k<psr)
  -- if $lanes($ishape($(psl * 2)) X psr, cv) = $ext(psl, $(psl * 2), sx, m)^psr

rule Step_read/vload-splat-oob:
  z; (CONST I32 i) (VLOAD (SPLAT n marg) x)  ~>  TRAP
  -- if $(i + marg.OFFSET + n/8) > |$mem(z, x).DATA|

rule Step_read/vload-splat-val:
  z; (CONST I32 i) (VLOAD (SPLAT n marg) x)  ~>  (VVCONST V128 cv)
  -- if $ibytes(n, m) = $mem(z, x).DATA[i + marg.OFFSET : n/8]
  -- if l = $(128/n)
  -- if $lanes($ishape(n) X l, cv) = m^l

rule Step_read/vload-zero-oob:
  z; (CONST I32 i) (VLOAD (ZERO n marg) x)  ~>  TRAP
  -- if $(i + marg.OFFSET + n/8) > |$mem(z, x).DATA|

rule Step_read/vload-zero-val:
  z; (CONST I32 i) (VLOAD (ZERO n marg) x)  ~>  (VVCONST V128 cv)
  -- if $ibytes(n, c) = $mem(z, x).DATA[i + marg.OFFSET : n/8]
  -- if cv = $ext(128, n, U, c)

rule Step_read/vload_lane-oob:
  z; (CONST I32 i) (VVCONST V128 cv_1) (VLOAD_LANE n x marg laneidx)  ~>  TRAP
  -- if $(i + marg.OFFSET + n/8 > |$mem(z, x).DATA|)

rule Step_read/vload_lane-val:
  z; (CONST I32 i) (VVCONST V128 cv_1) (VLOAD_LANE n x marg laneidx)  ~>  (VVCONST V128 cv)
  -- if $ibytes(n, m) = $mem(z, x).DATA[i + marg.OFFSET : n/8]
  -- if sh = $ishape(n) X $(128/n)
  -- if $lanes(sh, cv) = $lanes(sh, cv_1)[[laneidx] = m]


rule Step/store-num-oob:
  z; (CONST I32 i) (CONST nt c) (STORE nt x marg)  ~>  z; TRAP
  -- if $(i + marg.OFFSET + $size(nt)/8) > |$mem(z, x).DATA|

rule Step/store-num-val:
  z; (CONST I32 i) (CONST nt c) (STORE nt x marg)  ~>  $with_mem(z, x, $(i + marg.OFFSET), $($size(nt)/8), b*); eps
  -- if b* = $ntbytes(nt, c)

rule Step/store-pack-oob:
  z; (CONST I32 i) (CONST nt c) (STORE nt n x marg)  ~>  z; TRAP
  -- if $(i + marg.OFFSET + n/8) > |$mem(z, x).DATA|

rule Step/store-pack-val:
  z; (CONST I32 i) (CONST nt c) (STORE nt n x marg)  ~>  $with_mem(z, x, $(i + marg.OFFSET), $(n/8), b*); eps
  -- if b* = $ibytes(n, $wrap($size(nt), n, c))

rule Step/vstore-oob:
  z; (CONST I32 i) (VVCONST V128 cv) (VSTORE x marg)  ~>  z; TRAP
  -- if $(i + marg.OFFSET + $size(V128)/8) > |$mem(z, x).DATA|

rule Step/vstore-val:
  z; (CONST I32 i) (VVCONST V128 cv) (VSTORE x marg)  ~>  $with_mem(z, x, $(i + marg.OFFSET), $($size(V128)/8), b*); eps
  -- if b* = $vtbytes(V128, cv)

rule Step/vstore_lane-oob:
  z; (CONST I32 i) (VVCONST V128 cv) (VSTORE_LANE n x marg laneidx)  ~>  z; TRAP
  -- if $(i + marg.OFFSET + n) > |$mem(z, x).DATA|

rule Step/vstore_lane-val:
  z; (CONST I32 i) (VVCONST V128 cv) (VSTORE_LANE n x marg laneidx)  ~>  $with_mem(z, x, $(i + marg.OFFSET), $(n/8), b*); eps
  -- if b* = $ibytes(n, $lanes($ishape(n) X $(128/n), cv)[laneidx])


rule Step_read/memory.size:
  z; (MEMORY.SIZE x)  ~>  (CONST I32 n)
  -- if $(n * 64 * $Ki) = |$mem(z, x).DATA|


rule Step/memory.grow-succeed:
  z; (CONST I32 n) (MEMORY.GROW x)  ~>  $with_meminst(z, x, mi); (CONST I32 $(|$mem(z, x).DATA| / (64 * $Ki)))
  -- if mi = $growmemory($mem(z, x), n)

rule Step/memory.grow-fail:
  z; (CONST I32 n) (MEMORY.GROW x)  ~>  z; (CONST I32 $invsigned(32, $(-1)))


rule Step_read/memory.fill-oob:
  z; (CONST I32 i) val (CONST I32 n) (MEMORY.FILL x)  ~>  TRAP
  -- if $(i + n) > |$mem(z, x).DATA|

rule Step_read/memory.fill-zero:
  z; (CONST I32 i) val (CONST I32 n) (MEMORY.FILL x)  ~>  eps
  -- otherwise
  -- if n = 0

rule Step_read/memory.fill-succ:
  z; (CONST I32 i) val (CONST I32 n) (MEMORY.FILL x)  ~>
    (CONST I32 i) val (STORE I32 8 x $memarg0)
    (CONST I32 $(i+1)) val (CONST I32 $(n-1)) (MEMORY.FILL x)
  -- otherwise


rule Step_read/memory.copy-oob:
  z; (CONST I32 i_1) (CONST I32 i_2) (CONST I32 n) (MEMORY.COPY x_1 x_2)  ~>  TRAP
  -- if $(i_1 + n) > |$mem(z, x_1).DATA| \/ $(i_2 + n) > |$mem(z, x_2).DATA|

rule Step_read/memory.copy-zero:
  z; (CONST I32 i_1) (CONST I32 i_2) (CONST I32 n) (MEMORY.COPY x_1 x_2)  ~>  eps
  -- otherwise
  -- if n = 0

rule Step_read/memory.copy-le:
  z; (CONST I32 i_1) (CONST I32 i_2) (CONST I32 n) (MEMORY.COPY x_1 x_2)  ~>
    (CONST I32 i_1) (CONST I32 i_2) (LOAD I32 (8 _ U) x_2 $memarg0) (STORE I32 8 x_1 $memarg0)
    (CONST I32 $(i_1 + 1)) (CONST I32 $(i_2 + 1)) (CONST I32 $(n - 1)) (MEMORY.COPY x_1 x_2)
  -- otherwise
  -- if i_1 <= i_2

rule Step_read/memory.copy-gt:
  z; (CONST I32 i_1) (CONST I32 i_2) (CONST I32 n) (MEMORY.COPY x_1 x_2)  ~>
    (CONST I32 $(i_1+n-1)) (CONST I32 $(i_2+n-1)) (LOAD I32 (8 _ U) x_2 $memarg0) (STORE I32 8 x_1 $memarg0)
    (CONST I32 i_1) (CONST I32 i_2) (CONST I32 $(n-1)) (MEMORY.COPY x_1 x_2)
  -- otherwise


rule Step_read/memory.init-oob:
  z; (CONST I32 j) (CONST I32 i) (CONST I32 n) (MEMORY.INIT x y)  ~>  TRAP
  -- if $(i + n) > |$data(z, y).DATA| \/ $(j + n) > |$mem(z, x).DATA|

rule Step_read/memory.init-zero:
  z; (CONST I32 j) (CONST I32 i) (CONST I32 n) (MEMORY.INIT x y)  ~>  eps
  -- otherwise
  -- if n = 0

rule Step_read/memory.init-succ:
  z; (CONST I32 j) (CONST I32 i) (CONST I32 n) (MEMORY.INIT x y)  ~>
    (CONST I32 j) (CONST I32 $data(z,y).DATA[i]) (STORE I32 8 x $memarg0)
    (CONST I32 $(j+1)) (CONST I32 $(i+1)) (CONST I32 $(n-1)) (MEMORY.INIT x y)
  -- otherwise


rule Step/data.drop:
  z; (DATA.DROP x)  ~>  $with_data(z, x, eps); eps
